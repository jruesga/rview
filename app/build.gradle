/*
 * Copyright (C) 2016 Jorge Ruesga
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *      http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

apply plugin: 'com.android.application'
apply from: "${rootDir}/flavors.gradle"

import groovy.json.JsonSlurper

def libraries = rootProject.ext.libraries

dependencies {
    implementation project(':gerrit')
    implementation project(':attachments')
    implementation project(':wizard')
    implementation project(':drawer')
    implementation project(':aceeditor')
    implementation "androidx.legacy:legacy-preference-v14:${libraries.androidx_legacy}"
    implementation "androidx.browser:browser:${libraries.androidx_browser}"
    implementation "androidx.exifinterface:exifinterface:${libraries.androidx_exifinterface}"
    gmsImplementation "com.google.firebase:firebase-core:${libraries.firebase_core}"
    gmsImplementation "com.google.firebase:firebase-messaging:${libraries.firebase_messaging}"
    gmsImplementation "com.google.firebase:firebase-crashlytics:${libraries.firebase_crashlytics}"
    implementation "commons-io:commons-io:${libraries.commons_io}"
    implementation "com.github.bumptech.glide:glide:${libraries.glide}"
    annotationProcessor "com.github.bumptech.glide:compiler:${libraries.glide}"
    implementation "com.github.bumptech.glide:okhttp3-integration:${libraries.glide}"
    implementation "org.ocpsoft.prettytime:prettytime:${libraries.prettytime}"
    implementation "com.google.android:flexbox:${libraries.flexboxlayout}"
    implementation "com.github.jruesga:floatingsearchview:${libraries.floatingsearchview}"
    implementation ("com.caverock:androidsvg:${libraries.androidsvg}") {
        exclude group: 'org.jetbrains', module: 'annotations-java5'
    }
    implementation "net.yslibrary.keyboardvisibilityevent:keyboardvisibilityevent:${libraries.keyboardvisibilityevent}"
    implementation ("com.mikepenz:aboutlibraries:${libraries.aboutlibraries}@aar") { transitive = true }
    implementation "com.mikepenz:aboutlibraries-definitions:${libraries.aboutlibraries_definitions}"
    implementation "me.saket:better-link-movement-method:${libraries.better_link_movement_method}"

    // NOTE: Required for libraries generating old source (Glide, ...). Remove when no were required anymore
    implementation 'com.android.support:support-annotations:28.0.0'
    annotationProcessor 'com.android.support:support-annotations:28.0.0'
}

android {
    defaultConfig {
        versionCode 69
        versionName '2.4.2'
        vectorDrawables.useSupportLibrary = true

        applicationId 'com.ruesga.rview'

        // Firebase Messaging
        resValue "string", "fcm_project_id", "rview-43a4b"
        resValue "string", "fcm_app_id", "1:577867348267:android:01dee9b4cab97d4a"
        resValue "string", "fcm_sender_id", "577867348267"

        return void
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        release {
            if (rootProject.file('release.properties').exists()) {
                Properties releaseProps = new Properties()
                releaseProps.load(new FileInputStream(rootProject.file('release.properties')))

                Properties ksProps = new Properties()
                ksProps.load(new FileInputStream(new File((String) releaseProps['signing.config'])))
                storeFile file(ksProps['keystore'])
                keyAlias ksProps['alias']
                storePassword ksProps['storePass']
                keyPassword ksProps['pass']
                storeType ksProps['storeType']
            }

            return void
        }
    }

    dexOptions {
        jumboMode = true
    }

    buildTypes {
        debug {
            debuggable true

            applicationIdSuffix ".debug"

            ext.enableCrashlytics = false

            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt'), file('proguard-project-debug.txt')

            resValue "string", "fcm_api_key", "<empty>"
            resValue "bool", "fcm_enable_analytics", "false"
            resValue "string", "com.crashlytics.android.build_id", "<empty>"
            resValue "bool", "fcm_enable_crashlytics", "false"

            return void
        }
        release {
            debuggable false

            zipAlignEnabled true
            shrinkResources false
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), file('proguard-project.txt'), file('proguard-project-release.txt')

            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release

            // IMP! Enable your Firebase api key in a file called fabric.properties in the
            // project directory and add the value apiKey=<your key> to that file
            if (project.file('firebase.properties').exists()) {
                Properties firebaseProps = new Properties()
                firebaseProps.load(new FileInputStream(project.file('firebase.properties')))

                resValue "string", "google_app_id", firebaseProps["fcmAppId"]
                resValue "string", "fcm_api_key", firebaseProps["apiKey"]
                resValue "bool", "fcm_enable_analytics", "true"
                resValue "string", "com.crashlytics.android.build_id", firebaseProps["crashlyticsBuildId"]
                resValue "bool", "fcm_enable_crashlytics", "true"
            } else {
                resValue "string", "fcm_api_key", "<empty>"
                resValue "bool", "fcm_enable_analytics", "false"
                resValue "string", "com.crashlytics.android.build_id", "<empty>"
                resValue "bool", "fcm_enable_crashlytics", "false"
            }
            return void
        }
    }

    return void
}

def addActivityAliases(manifest) {
    if (!manifest.exists()) {
        throw new groovy.util.ResourceException("Cannot replace activity aliases. Manifest doesn't exists: $manifest")
    }

    def repositories = new File(
            (project.path + "/src/main/res/raw/repositories.json").replace(":", ""))

    def json = new JsonSlurper().parseText(repositories.text)
    def activityAlias = "\n\n<!-- Repositories url handling activity-aliases -->\n"
    json.each {
        def url = new URL(it.url.toString())
        def host = url.authority
        def path = url.path
        def name = it.name.toString().replaceAll(" ", "")

        activityAlias +=
            "<activity-alias android:name=\".${name}ExternalUrlHandlerActivity\"\n" +
            "    android:theme=\"@style/AppTheme.NoStatusBarColor\"\n" +
            "    android:targetActivity=\"com.ruesga.rview.UrlHandlerProxyActivity\"\n" +
            "    android:excludeFromRecents=\"true\"\n" +
            "    android:taskAffinity=\"\"\n" +
            "    android:enabled=\"false\">\n" +
            "    <intent-filter>\n" +
            "        <action android:name=\"android.intent.action.VIEW\" />\n" +
            "        <category android:name=\"android.intent.category.DEFAULT\" />\n" +
            "        <category android:name=\"android.intent.category.BROWSABLE\" />\n" +
            "        <data android:scheme=\"http\" android:host=\"${host}\" android:pathPrefix=\"${path}\" />\n" +
            "        <data android:scheme=\"https\" android:host=\"${host}\" android:pathPrefix=\"${path}\" />\n" +
            "    </intent-filter>\n" +
            "</activity-alias>\n\n"
    }

    def text = manifest.text.replace("<!-- URL-ACTIVITY-HANDLERS -->", activityAlias.toString())
    manifest.delete()
    manifest.createNewFile()
    manifest.text += text
}

def registerAddActivityAliases(task, flavor, build) {
    def flavorName = flavor.name.toString()
    def buildName = build.name.toString()
    def manifestFull = new File(
            (project.path + "/build/intermediates/merged_manifests/${flavorName}${buildName.capitalize()}/AndroidManifest.xml")
            .replace(":", ""))

    def addActivityAliasesTaskName = "addActivityAliases${flavorName.capitalize()}${buildName.capitalize()}"
    project.task(addActivityAliasesTaskName).doLast({
        description = "Include extra activity-alias to final AndroidManifest.xml [${flavorName} / ${buildName}]"
        addActivityAliases(manifestFull)
    })

    task.dependsOn(addActivityAliasesTaskName)
    def processTask = "process${flavorName.capitalize()}${buildName.capitalize()}Manifest"
    project.(addActivityAliasesTaskName.toString()).dependsOn(processTask)
}

tasks.whenTaskAdded { task ->
    project.("android").productFlavors.all { flavor ->
        def flavorName = flavor.name.capitalize().toString()
        project.("android").buildTypes.all { build ->
            def buildName = build.name.capitalize().toString()
            def compileTask = "process${flavorName}${buildName}Resources".toString()
            if (compileTask == task.name) {
                registerAddActivityAliases(task, flavor, build)
            }
        }
    }

}

apply plugin: 'com.getkeepsafe.dexcount'
